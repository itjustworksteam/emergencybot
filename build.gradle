apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    systemProperty "file.encoding", "utf-8"
}

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
  }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'info.solidsoft.pitest'

javadoc {
    options {
        links( 'http://docs.oracle.com/javase/8/docs/api/' )
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        charSet 'UTF-8'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.restlet.org" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'org.restlet.jse:org.restlet:2.3.9'
    compile 'org.restlet.jse:org.restlet.ext.jackson:2.3.9'
    compile 'com.github.pengrad:java-telegram-bot-api:2.3.1'
    compile 'com.github.itjustworksteam:javaemergencyapi:0.0.3'
    compile 'com.vdurmont:emoji-java:3.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile 'org.mockito:mockito-core:2.7.14'
}

pitest {
    targetClasses = [ "it.itjustworks.emergencybot.*" ]
}

shadowJar {
    archiveName 'emergencybot.jar'
}

task stage() {
    dependsOn clean, shadowJar
}

tasks.stage.doLast() {
    delete 'build/classes'
    delete 'build/dependency-cache'
    delete 'build/tmp'
    delete fileTree(dir: 'build/libs', exclude: '*.jar')
}

shadowJar.mustRunAfter clean
